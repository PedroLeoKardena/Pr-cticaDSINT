//created on: 25 oct 2025
package rule1

//list any import classes here.
import java.util.List;
import java.util.Collections;
import java.util.Comparator;
import java.util.ArrayList;

import dominioECG.Persona;
import dominioECG.RegistroECG;
import dominioECG.Onda;
import dominioECG.OndaP;
import dominioECG.OndaQ;
import dominioECG.OndaR;
import dominioECG.OndaS;
import dominioECG.OndaT;
import dominioECG.Ciclo;
import dominioECG.Intervalo;
import dominioECG.TipoIntervalo;
import dominioECG.Complejo;
import dominioECG.RitmoCardiaco;
import dominioECG.Diagnostico;
import dominioECG.Bradicardia;
import dominioECG.Taquicardia;
import dominioECG.ContraccionVentricularPrematura;
import dominioECG.Hipocalemia;
import dominioECG.Hipopotasemia;
import dominioECG.IAM;
import dominioECG.Isquemia;
import dominioECG.Saludable;

//declare any global variables here
global java.util.concurrent.atomic.AtomicInteger cicloContador;



rule "Print y Crear Registros Persona"
		no-loop true
		agenda-group "Reglas-Inicio"
    when
        $p: Persona();
    then
    	java.util.List<RegistroECG> registros = $p.getRegistroECG();
        for (RegistroECG reg : registros) {
	        System.out.println("Registro ID: " + reg.getIdRegistro());
	        System.out.println("Insertando registro clase: " + reg.getClass().getName());
	        insert(reg);
    	}
end

declare RegistroProcesado
    registroId : String
end

declare ReglasInicioProcesadas
    registroId : String
end


rule "Insertar ondas"
		agenda-group "Reglas-Inicio"
    when
        $r: RegistroECG();
        not RegistroProcesado( registroId == $r.getIdRegistro() )
    then
	    System.out.println("Insertamos ondas del registro con ID: " + $r.getIdRegistro() + ".");
	    List<Onda> ondas= $r.getOndasRegistro();

	    for (int i = 0; i < ondas.size() - 1; i++) {
	        insert(ondas.get(i));
	    }
		insert(new RegistroProcesado($r.getIdRegistro()));
end

rule "Crear ciclos"
		no-loop true
		agenda-group "Reglas-Inicio"
	when
        // Capturamos un registro para usarlo en todos los filtros
        $registro : RegistroECG()
		not ReglasInicioProcesadas( registroId == $registro.getIdRegistro() )
        
		// Encontramos OndaP
        // Debe ser del registro, sin ciclo asociado. Guardamos el start time y el end time para hacer 
        //comprobaciones con ondaQ
        $p : OndaP(
                registroPertenece == $registro, // Debe pertenecer a este $registro
                cicloPertenece == null,
                $finp : end_time
              )
        
        // Encontramos OndaQ (margen 70-120ms desde inicio OndaP)
       	$q : OndaQ(
                registroPertenece == $registro, // Mismo registro
                cicloPertenece == null,
                start_time >= $finp,
	            start_time <= $finp + 110,
	            $finq : end_time 
              )
              
        // Encontramos OndaR (margen Q-R: ±5ms)
        $r : OndaR(
                registroPertenece == $registro, // Mismo registro
                cicloPertenece == null,
                start_time >= $finq,
                start_time <= $finq + 20,
                $finr : end_time
              )
              
        // Encontramos OndaS (margen R-S: ±5ms) 
        $s : OndaS(
                registroPertenece == $registro, // Mismo registro
                cicloPertenece == null,
                start_time >= $finr,
                start_time <= $finr + 20,
                $fins : end_time
              )
        
        // Encontramos OndaT
        $t : OndaT(
                registroPertenece == $registro, // Mismo registro
                cicloPertenece == null,
                start_time >= $fins + 120,
	            start_time <= $fins + 260
              )     
         
	then	
		int nCiclos = cicloContador.incrementAndGet();
        System.out.println(" Creando Ciclo " + nCiclos + " para el registro " + $registro.getIdRegistro() + ".");
        
		Ciclo ciclo = new Ciclo();
		
		ciclo.setWaveP($p);
		ciclo.setWaveQ($q);
		ciclo.setWaveR($r);
		ciclo.setWaveS($s);
		ciclo.setWaveT($t);
		ciclo.setCycle_number(nCiclos);
		
		ciclo.setRegistroPertenece($registro);
		
		modify($p){setCicloPertenece(ciclo)};
		modify($q){setCicloPertenece(ciclo)};
		modify($r){setCicloPertenece(ciclo)};
		modify($s){setCicloPertenece(ciclo)};
		modify($t){setCicloPertenece(ciclo)};
		
		//Modificamos tambien registro, ya que le tenemos que meter el registro
		modify($registro){addCiclo(ciclo)};
		
				
		//Insertamos ciclo
		insert(ciclo);
end


rule "Crear intervalos PP Persona" //Dos ciclos, en estos intervalos no calcularemos el incremento de amplitud, le daremos valor 0.
    	agenda-group "Reglas-Inicio"
    when
    	$r: RegistroECG()
		not ReglasInicioProcesadas( registroId == $r.getIdRegistro() )
    	$p1 : OndaP(
	        registroPertenece == $r, // Comprueba que pertenece a $r
	        cicloPertenece != null,
	        $nciclo : cicloPertenece.cycle_number 
	    )
	    $p2 : OndaP(
	        registroPertenece == $r,
	        cicloPertenece != null,
	        cicloPertenece.cycle_number == $nciclo + 1 
	    )
    then
	    System.out.println("Creacion intervalo PP para ciclos: " + $nciclo + " y "+ $p2.getCicloPertenece().getCycle_number() + " del registro: " + $r.getIdRegistro());
	    Intervalo intervaloPP = new Intervalo($p1.getStart_time(), $p2.getStart_time(), TipoIntervalo.PP);
	    intervaloPP.setRegistroPertenece($r);
	    insert(intervaloPP);
end

rule "Crear intervalos RR Persona" //Dos ciclos, en estos intervalos no calcularemos el incremento de amplitud, le daremos valor 0.
    	agenda-group "Reglas-Inicio"
    when
    	$r: RegistroECG()
    	not ReglasInicioProcesadas( registroId == $r.getIdRegistro() )
    	$r1 : OndaP(
	        registroPertenece == $r, // Comprueba que pertenece a $r
	        cicloPertenece != null,
	        $nciclo : cicloPertenece.cycle_number 
	    )
	    $r2 : OndaP(
	        registroPertenece == $r,
	        cicloPertenece != null,
	        cicloPertenece.cycle_number == $nciclo + 1 
	    )
    then
	    System.out.println("Creacion intervalo RR para ciclos: " + $nciclo + " y "+ $r2.getCicloPertenece().getCycle_number() + " del registro: " + $r.getIdRegistro());
	    Intervalo intervaloRR = new Intervalo($r1.getStart_time(), $r2.getStart_time(), TipoIntervalo.RR);
	    intervaloRR.setRegistroPertenece($r);
	    insert(intervaloRR);
end

rule "Crear intervalos PR Persona" //Dos ciclos
    	agenda-group "Reglas-Inicio"
    when
    	$r: RegistroECG()
    	not ReglasInicioProcesadas( registroId == $r.getIdRegistro() )
    	$p : OndaP(
            registroPertenece == $r, // Debe pertenecer a este $registro
            cicloPertenece != null,
            $nciclo : cicloPertenece.cycle_number
          )
    	$wR: OndaR(
    		registroPertenece == $r,
    		cicloPertenece != null,
    		cicloPertenece.cycle_number == $nciclo
    	)
    then
	    System.out.println("Creacion intervalo PR para ciclo: " + $nciclo + " del registro: " + $r.getIdRegistro());
	    Intervalo intervaloPR = new Intervalo($p.getStart_time(), $wR.getStart_time(), TipoIntervalo.PR);
	    intervaloPR.setRegistroPertenece($r);
	    insert(intervaloPR);
end


rule "Crear intervalos QT Persona" //Dos ciclos
    	agenda-group "Reglas-Inicio"
    when
    	$r: RegistroECG()
    	not ReglasInicioProcesadas( registroId == $r.getIdRegistro() )
    	$q : OndaQ(
                registroPertenece == $r, // Debe pertenecer a este $registro
                cicloPertenece != null,
                $nciclo : cicloPertenece.cycle_number
              )
    	$t: OndaT(
    		registroPertenece == $r,
    		cicloPertenece != null,
    		cicloPertenece.cycle_number == $nciclo
    	)
    then
	    System.out.println("Creacion intervalo QT para ciclo: " + $nciclo + " del registro: " + $r.getIdRegistro());
	    Intervalo intervaloQT = new Intervalo($q.getStart_time(), $t.getEnd_time(), TipoIntervalo.QT);
	    intervaloQT.setRegistroPertenece($r);
	    insert(intervaloQT);
end


rule "Crear intervalos ST Persona (segmentos)"
    	agenda-group "Reglas-Inicio"
    when
    	$r: RegistroECG()
    	not ReglasInicioProcesadas( registroId == $r.getIdRegistro() )
    	$s : OndaS(
                registroPertenece == $r, // Debe pertenecer a este $registro
                cicloPertenece != null,
                $nciclo : cicloPertenece.cycle_number,
                $amplitudS : peak_amplitude
              )
    	$t: OndaT(
	    		registroPertenece == $r,
	    		cicloPertenece != null,
	    		cicloPertenece.cycle_number == $nciclo,
	    		$amplitudT : peak_amplitude
	    	)
    then
	    System.out.println("Creacion intervalo ST para ciclo: " + $nciclo + " del registro: " + $r.getIdRegistro());
	    Intervalo intervaloST = new Intervalo($s.getEnd_time(), $t.getStart_time(), TipoIntervalo.ST);
	    intervaloST.setRegistroPertenece($r);
	    insert(intervaloST);
end

rule "Crear intervalos TP Persona" //Dos ciclos
    	agenda-group "Reglas-Inicio"
    when
    	$r: RegistroECG()
    	not ReglasInicioProcesadas( registroId == $r.getIdRegistro() )
    	$t : OndaP(
                registroPertenece == $r, // Debe pertenecer a este $registro
                cicloPertenece != null,
                $nciclo : cicloPertenece.cycle_number
              )
    	$p: OndaP(
    		registroPertenece == $r,
    		cicloPertenece != null,
    		cicloPertenece.cycle_number == $nciclo+1
    	)
    then
	    System.out.println("Creacion intervalo TP para ciclos: " + $nciclo + " y "+ $p.getCicloPertenece().getCycle_number() + " del registro: " + $r.getIdRegistro());
	    Intervalo intervaloTP = new Intervalo($t.getStart_time(), $p.getStart_time(), TipoIntervalo.TP);
	    intervaloTP.setRegistroPertenece($r);
	    insert(intervaloTP);
end

rule "Crear complejo QRS"
		agenda-group "Reglas-Inicio"
	when
	    $r: RegistroECG()
	    not ReglasInicioProcesadas( registroId == $r.getIdRegistro() )
    	$q : OndaQ(
            registroPertenece == $r, // Debe pertenecer a este $registro
            cicloPertenece != null,
            $nciclo : cicloPertenece.cycle_number
        )
        $wR: OndaR(
    		registroPertenece == $r,
    		cicloPertenece != null,
    		cicloPertenece.cycle_number == $nciclo
    	)
    	$s : OndaS(
            registroPertenece == $r, // Debe pertenecer a este $registro
            cicloPertenece != null,
            cicloPertenece.cycle_number == $nciclo
          )
	then
		System.out.println("Creacion complejo QRS del ciclo: " + $nciclo + " del registro: " + $r.getIdRegistro());
	    
	    List<Onda> ondas = new ArrayList<>();
        ondas.add($q);
        ondas.add($wR);
        ondas.add($s);

        int tiempoInicio = $q.getStart_time();
        int tiempoFin = $s.getEnd_time();

        Complejo qrs = new Complejo(ondas, tiempoInicio, tiempoFin);
        qrs.setRegistroAsociado($r);
        insert(qrs);
end

rule "Crear Frecuencia Cardiaca"
	when
		$r: RegistroECG()
		accumulate (Intervalo(tipoIntervalo == TipoIntervalo.RR, registroPertenece == $r, $dur : duracion),
				    $promRR : average($dur)
				)
		not( RitmoCardiaco( registroAsignado == $r ))   
	then
		double bpm = 60000/($promRR);
		System.out.println("Ritmo cardiaco del registro: " + $r.getIdRegistro() + " = " + bpm);
		RitmoCardiaco rc = new RitmoCardiaco(bpm, $r);
		insert(rc);
		modify($r){setFrecuenciaCardiaca(rc)};
		//Todas las reglas con prioridad alta se han ejecutado, insertamos hecho de marca.
		insert(new ReglasInicioProcesadas($r.getIdRegistro()));	
end

rule "Detectar Bradicardia Severa"
		no-loop true
	when
		$r: RegistroECG();
		//Comprobamos que no tenga Taquicardia por la restricción.
		not( Taquicardia( registroECG == $r ) )
		not( Bradicardia( registroECG == $r ) )
		$rc: RitmoCardiaco(registroAsignado == $r,
						   valor <= 40)
	then
		String nombreDiagnostico = new String("Bradicardia Severa");
		String descripcion = new String("Detectada bradicardia sinusal SEVERA con respecto al registro: " + $r.getIdRegistro() + " por un ritmo cardiaco cuya frecuencia es inferior a 40 latidos/minuto en reposo: " + $rc.getValor());
		Bradicardia bradi = new Bradicardia(descripcion, nombreDiagnostico);
		bradi.setRegistroAsociado($r);
		bradi.setFrecuencia($rc);
		insert(bradi);
		System.out.println(nombreDiagnostico);		
		modify($r){addDiagnostico(bradi)};
		
end

rule "Detectar Bradicardia no Severa"
		no-loop true
	when
		$r: RegistroECG();
		//Comprobamos que no tenga Taquicardia por la restricción.
		not( Taquicardia( registroECG == $r ) )
		not( Bradicardia( registroECG == $r ) )
		$rc: RitmoCardiaco(registroAsignado == $r,
						   valor > 40,
						   valor < 60)
	then
		String nombreDiagnostico = new String("Bradicardia no Severa");
		String descripcion = new String("Detectada bradicardia sinusal con respecto al registro: " + $r.getIdRegistro() + " por un ritmo cardiaco cuya frecuencia es inferior a 60 latidos/minuto en reposo: " + $rc.getValor());
		Bradicardia bradi = new Bradicardia(descripcion, nombreDiagnostico);
		bradi.setRegistroAsociado($r);
		bradi.setFrecuencia($rc);
		insert(bradi);
		System.out.println(nombreDiagnostico);
		modify($r){addDiagnostico(bradi)};
				
end

rule "Detectar Taquicardia"
		no-loop true
	when
		$r: RegistroECG();
		not( Taquicardia( registroECG == $r ) )
		//Comprobamos que no tenga Bradicardia por la restriccion.
		not( Bradicardia( registroECG == $r ) )
		$rc: RitmoCardiaco(registroAsignado == $r,
						   valor > 100)
	then
		String nombreDiagnostico = new String("Taquicardia");
		String descripcion = new String("Detectada taquicardia sinusal con respecto al registro: " + $r.getIdRegistro() + " por un ritmo cardiaco cuya frecuencia es superior a los 100 latidos/minuto en reposo: " + $rc.getValor());
		Taquicardia taqui = new Taquicardia(descripcion, nombreDiagnostico);
		taqui.setRegistroAsociado($r);
		taqui.setFrecuencia($rc);
		insert(taqui);
		System.out.println(nombreDiagnostico);
		modify($r){addDiagnostico(taqui)};
		
end

rule "Detectar hipocalcemia"
		no-loop true
	when
		//Esta regla solo se apliara si sucede lo siguiente: Si tenemos un segmento prolongado:
		//QT > 460 (en el caso de mujeres) o QT > 440 (en el caso de hombres).
	    Persona( $sexo : sexo )
	    $r: RegistroECG();
		not( Hipocalemia( registroECG == $r ) )
			    
	    $q : OndaQ(
                cicloPertenece != null,
                $inicioQ : start_time,
                $nciclo : cicloPertenece.cycle_number
              )
              
	    $intervaloQT : Intervalo(
	        registroPertenece == $r,
	        tipoIntervalo == TipoIntervalo.QT,
	        tiempoInicio == $inicioQ,
	        // Aplicamos umbrales diferentes segun el sexo
	        (($sexo == "F" && duracion > 460) || 
	         ($sexo == "M" && duracion > 440))
	    )
	    
	then
		String nombreDiagnostico = new String("Hipocalcemia");
	    String descripcion = new String(
	        "Detectado patron sugestivo de Hipocalcemia en el registro: " + $r.getIdRegistro() + 
	        ".\nCaracteristicas: " +
	        "- Intervalo QT prolongado (" + $intervaloQT.getDuracion() + " ms > " + 
	          ($sexo == "F" ? "460" : "440") + " ms). " +
	        "- Este patron electrocardiografico sugiere posible hipocalcemia.\n" +
	        "- Ciclo afectado: " + $nciclo + ".\n" +
	        "La hipocalcemia prolonga el intervalo QT principalmente por alargamiento del segmento ST, " +
	        "lo que aumenta el riesgo de arritmias ventriculares. Se recomienda confirmar con niveles sericos de calcio ionizado."
	    );
	    
	    Hipocalemia diagnosticoHipocalcemia = new Hipocalemia(descripcion, nombreDiagnostico);
	    diagnosticoHipocalcemia.setRegistroAsociado($r);
	    diagnosticoHipocalcemia.setnCicloAfectado($nciclo);
	    insert(diagnosticoHipocalcemia);
    	System.out.println(nombreDiagnostico);
    	modify($r){addDiagnostico(diagnosticoHipocalcemia)};
end

rule "Detectar hipopotasemia"
		no-loop true
	when
	    $r: RegistroECG();
	    
	    not( Hipopotasemia( registroECG == $r ) )
	
	    // Ondas del mismo ciclo
	    $p : OndaP(
	            registroPertenece == $r,
	            cicloPertenece != null,
	            $ciclo : cicloPertenece
	    )
	
	    $t : OndaT(
	            registroPertenece == $r,
	            cicloPertenece == $ciclo,
	            $amplitudT : peak_amplitude
	    )
	
	    eval(
	        // T muy negativa (hipopotasemia severa)
	        $amplitudT <= -10
			)
			
	then
	    String nombreDiagnostico = "Hipopotasemia";
	    String descripcion = "Detectado patron sugestivo de Hipopotasemia severa en el registro: " +
	                     $r.getIdRegistro() + ".\nCiclo: " + $ciclo.getCycle_number() +
	                     ". Características: Amplitud T extremadamente negativa: " +
	                     String.format("%.3f", $amplitudT) + " mV.";
	
	
	    Hipopotasemia diagnostico = new Hipopotasemia(descripcion, nombreDiagnostico);
	    diagnostico.setRegistroAsociado($r);
	    diagnostico.setnCicloAfectado($ciclo.getCycle_number());
	
	    insert(diagnostico);
	    System.out.println(nombreDiagnostico);
	    modify($r){addDiagnostico(diagnostico)};
end



rule "Detectar PVC"
		no-loop true
	when
		$r: RegistroECG()
		
		not( ContraccionVentricularPrematura( registroECG == $r ) )

		accumulate (Intervalo(tipoIntervalo == TipoIntervalo.RR, registroPertenece == $r, $dur : duracion),
				    $promRR : average($dur)
				)
		//Calculamos promedio para saber si hay Prematuridad del QRS (RR_anterior < 0.8 × RR_promedio)
		
		$p : OndaP( registroPertenece == $r, 
					cicloPertenece != null ,
					$nciclo : cicloPertenece.cycle_number, 
					$finP : end_time )
		//Onda P para detectar si hay Ausencia de onda P previa
		
		$qrs : Complejo(
		    registroAsociado == $r,
		    $inicioQRS : tiempoInicio,
		    $finQRS : tiempoFin,
		    $durQRS : duracion,
		    $ondas : ondasCompuesto 
		)
		
		// Extraer onda R del complejo con verificación de tipo segura
	    $onda : Onda() from $ondas
	    eval($onda instanceof dominioECG.OndaR)
	    $ondaRcomp: dominioECG.OndaR(this == $onda, cicloPertenece != null, $cicloR : cicloPertenece)			
		
		// Comprobamos que el complejo es del mismo ciclo que la onda P (con verificación de null)
		eval($nciclo == $cicloR.getCycle_number())
			
		$rr_prev : Intervalo(
	        registroPertenece == $r,
	        tipoIntervalo == TipoIntervalo.RR,
	        $durRR : duracion,
	        // suponiendo que la onda R final de este RR es la que precede al inicio de la actual
	        $finRR : tiempoFin
   		)
   		
   		//Comprobamos que se trata del intervalo RR anterior al complejo QRS.
	    eval(Math.abs($ondaRcomp.getStart_time() - $finRR) < 0.001)
	  		
	    eval($durRR < 0.8 * $promRR) // prematuridad
    	eval($durQRS > 120)          // QRS ancho
    	eval($inicioQRS - $finP > 0.2 * $promRR) // separación excesiva → sin P previa
    	
	then
		String nombreDiagnostico = new String("Contraccion Ventricular Prematura");
		String descripcion = new String(
		    "Detectada Contraccion Ventricular Prematura (PVC) en el registro: " + $r.getIdRegistro() + 
		    ".\nCaracterísticas: " +
		    "- Intervalo RR anterior (" + $durRR + " ms) es prematuro (< 80% del promedio RR: " + $promRR + " ms).\n" +
		    "- Complejo QRS ancho (" + $durQRS + " ms > 100 ms).\n" +
		    "- Ausencia de onda P previa (separación OndaP-QRS: " + ($inicioQRS - $finP) + " ms > 20% del RR promedio).\n" +
		    "* Ciclo afectado: " + $nciclo + ".\n" +
		    "Esto indica un latido de origen ventricular que interrumpe el ritmo cardiaco normal."
		);
		
		ContraccionVentricularPrematura cvp = new ContraccionVentricularPrematura(descripcion, nombreDiagnostico);
		cvp.setRegistroAsociado($r);
		cvp.setnCicloAfectado($nciclo);
		
		insert(cvp);
		System.out.println(nombreDiagnostico);
		modify($r){addDiagnostico(cvp)};
end

rule "Detectar Isquemia Coronaria"
		no-loop true
	when
	// Para detectar Isquemia Coronaria nos tenemos que fijar principalmente en el segmento ST y en la onda T, el resto del 
	// electrocardiograma no nos da informacion util para detectarla. Detecta una Isquemia Coronaria la depresion del segmento ST y la
	// inversion de la onda T.
	
	// En cuanto a la diferencia entre hombres y mujeres si que hay solo que no en el ECG.
    	$r: RegistroECG()
    
    	// Evitar diagnóstico duplicado
    	not( Isquemia( registroECG == $r ) )
    
    	$s : OndaS(
        	registroPertenece == $r,
        	cicloPertenece != null,
        	$nciclo : cicloPertenece.cycle_number,
        	$finS : end_time,
        	$ampS : peak_amplitude
    	)
    
    	$t : OndaT(
        	registroPertenece == $r,
        	cicloPertenece != null,
        	cicloPertenece.cycle_number == $nciclo,
        	$inicioT : start_time,
        	$ampT : peak_amplitude
    	)
    
    	// Criterios para isquemia: depresión ST o inversión de onda T
    	eval(
    	    $ampT > -10 &&               // Evita conflicto con hipopotasemia severa
    	    ( ($ampT - $ampS) <= -0.1    
    	      || $ampT < -0.1 )          
    	)
    
	then
    	String nombreDiagnostico = "Isquemia Coronaria";
    	double diferenciaST = $ampT - $ampS;
    
    	String descripcion = "Detectado patron sugestivo de Isquemia Coronaria en el registro: " + 
                        	$r.getIdRegistro() + ".\nCiclo: " + $nciclo + 
                        	". Caracteristicas: Depresion ST: " + String.format("%.3f", diferenciaST) + 
                        	" mV, Amplitud T: " + String.format("%.3f", $ampT) + " mV.";
    
    	Isquemia isquemia = new Isquemia(descripcion, nombreDiagnostico);
    	isquemia.setRegistroAsociado($r);
    	isquemia.setnCicloAfectado($nciclo);
    
    	insert(isquemia);
    	System.out.println(nombreDiagnostico);
    	modify($r){addDiagnostico(isquemia)};
end

rule "Detectar Infarto Agudo de Miocardio." 
		no-loop true
	when
		//Tenemos que detectar ondas T extremadamentes agudas. Para ello buscamos ondas T cuya amplitud absoluta en
		//comparación con la amplitud absoluta total de las ondas del complejo QRS de ese mismo ciclo sea mayor de un
		//36%. Luego debemos observar que esta onda T tenga un valor de amplitud de pico superior a 0.8 mV para mujeres
		//y 1 mV para hombres.
		Persona( $sexo : sexo )
		 
		$r: RegistroECG()
		
		// Verificamos que NO existe ya un diagnóstico IAM para este registro
    	not( IAM( registroECG == $r ) )
		
		$t : OndaT( registroPertenece == $r, 
					cicloPertenece != null ,
					$nciclo : cicloPertenece.cycle_number,
					( $sexo == "F" && peak_amplitude > 0.8 ) || ( $sexo == "M" && peak_amplitude > 1.0 ), 
					$amplitudT : peak_amplitude,
					$inicioT : start_time)
		
		//Comprobamos que este complejo sea del mismo ciclo que T fijandonos en el tiempo de inicio de T
		$qrs : Complejo(
		    registroAsociado == $r,
		    $ondas : ondasCompuesto,
		    tiempoFin <= $inicioT-120,
		    tiempoFin >= $inicioT-260
		)
	
		//Calculamos la amplitud absoluta de QRS
		$sumAmplitudQRS : Double() from accumulate(
		    Onda($amp : peak_amplitude) from $ondas,
		    sum(Math.abs($amp))
		)
		
		//Hacemos la evaluacion
		eval( ($amplitudT / $sumAmplitudQRS) >= 0.36 )
	then
		String nombreDiagnostico = new String("Infarto Agudo de Miocardio (IAM)");

	    // Construimos la descripción
	    String descripcion = new String("Detectado Infarto Agudo de Miocardio (IAM) en el registro: " + $r.getIdRegistro() +
	                         ".\nCaracteristicas: " +
	                         "- Onda T hiperaguda en el ciclo " + $nciclo + " (amplitud: " + $amplitudT + " mV).\n" +
	                         "- Complejo QRS del mismo ciclo con amplitud absoluta total: " + $sumAmplitudQRS + " mV.\n" +
	                         "- Ratio T/QRS: " + String.format("%.2f", ($amplitudT / $sumAmplitudQRS)*100) + "% (>36%).\n" +
	                         "- Umbral de amplitud de T: " + ($sexo.equals("F") ? "0.8 mV" : "1.0 mV") + ".\n" +
	                         "Esto indica una onda T extremadamente aguda, sugestiva de IAM.");
		// Creamos el objeto IAM y asociamos información
	    IAM iam = new IAM(descripcion, nombreDiagnostico);
	    iam.setRegistroAsociado($r);
	    iam.setnCicloAfectado($nciclo);
	    iam.setAmplitudT($amplitudT);
    	iam.setAmplitudQRS($sumAmplitudQRS);
    	
    	insert(iam);
		System.out.println(nombreDiagnostico);
		modify($r){addDiagnostico(iam)};
	                         
end

rule "Registro Sin Diagnostico"
		no-loop true
		salience -10
	when
		$r : RegistroECG()
		//Vemos si el registro tiene diagnostico asignado, si no, decimos que es saludable.
		not( Diagnostico( registroECG == $r ) ) 
	then
		String nombreDiagnostico = new String("Saludable");

	    String descripcion = new String(
	        "No se han detectado anomalias significativas en el registro: " + $r.getIdRegistro() + ".\n" +
	        "Las ondas P, los complejos QRS y las ondas T presentan una morfologia, amplitud y duración " +
	        "compatibles con los parámetros fisiologicos esperados.\n" +
	        "No se han observado arritmias, alteraciones en la repolarización, elevaciones o depresiones del ST, " +
	        "ni irregularidades en los intervalos RR que sugieran patología.\n" +
	        "El trazado electrocardiográfico es homogeneo, estable y se interpreta como un registro saludable."
	    );
	
	    Saludable diag = new Saludable(descripcion, nombreDiagnostico);
	    diag.setRegistroAsociado($r);
	    insert(diag);
	    System.out.println(nombreDiagnostico);
		modify($r){addDiagnostico(diag)};
	
end
	

